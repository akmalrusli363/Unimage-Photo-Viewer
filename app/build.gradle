plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

def apikeyPropertiesFile = rootProject.file("apikey.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.tilikki.training.unimager.demo"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.tilikki.training.unimager.demo.AppTestRunner"

        buildConfigField("String", "CLIENT_ID", apikeyProperties['CLIENT_ID'])
        buildConfigField("String", "CLIENT_SECRET", apikeyProperties['CLIENT_SECRET'])
        buildConfigField("String", "ACCESS_TOKEN", apikeyProperties['ACCESS_TOKEN'])
    }
    buildFeatures {
        dataBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    sourceSets {
        test {
            java.srcDirs += "$projectDir/src/testShared"
        }

        androidTest {
            java.srcDirs += "$projectDir/src/testShared"
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
// Kotlin and Android cores
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.core:core-ktx:1.3.2"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation 'androidx.multidex:multidex:2.0.1'

    // Android UI
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "com.google.android.material:material:1.3.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation 'androidx.recyclerview:recyclerview:1.2.0'

    // Square
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:2.2.21"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"

    // Image Loader
    def picasso_version = "2.71828"
    implementation "com.squareup.picasso:picasso:$picasso_version"

    // Dagger
    def dagger_version = "2.35.1"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // Room dependency
    def room_version = "2.2.6"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Debug for Testing.
    // NOTE: This library may not work if declared as androidTestImplementation as
    // the occurred bug reported in https://issuetracker.google.com/128612536.
    debugImplementation "androidx.fragment:fragment-testing:1.3.6"

    // Test Libraries
    testImplementation 'junit:junit:4.13.2'
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "androidx.test:core:1.3.0"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation "androidx.test:runner:1.3.0"

    // Mockito
    def mockito_core_version = "3.9.0"
    testImplementation "org.mockito:mockito-core:$mockito_core_version"
    testImplementation "org.mockito.kotlin:mockito-kotlin:3.2.0"
    androidTestImplementation "org.mockito:mockito-core:$mockito_core_version"
    androidTestImplementation "org.mockito:mockito-android:$mockito_core_version"

    // Espresso
    def espresso_version = "3.3.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espresso_version"

    // Dagger for Android Test
    kaptAndroidTest "com.google.dagger:dagger-android-processor:$dagger_version"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger_version"
}
